USE [DB_Pro]
GO
/****** Object:  StoredProcedure [dbo].[spBinStorageGridView]    Script Date: 4/18/2023 4:59:32 PM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[spBinStorageGridView]
(
	   @TblCategoryId	BinStorageCategoryIdType	READONLY	,
	   @BinNameId	INT			,
	   @KeyWord			VARCHAR(50)	
)
AS 

BEGIN TRY 
	SET NOCOUNT ON;
	DECLARE @ParameterList varchar(8000) =	'ProcedureName=DB.dbo.spBinStorageGridView'	

	IF EXISTS (SELECT 1 FROM @TblCategoryId)
	BEGIN
		SELECT ItemDescription, BinName, Categories, BinStorageID, BinNameID
		FROM (
			SELECT bs.Descr AS ItemDescription,
				   bn.Descr AS BinName,
				   bs.BinNameID,
				   bs.BinStorageID,
				   STUFF((SELECT ', ' + bc.Descr
						  FROM BinTags bt
						  JOIN BinCategory bc 
						  ON bc.BinCategoryID = bt.BinCategoryID
						  WHERE bt.BinStorageID = bs.BinStorageID
						  FOR XML PATH('')), 1, 2, '') AS Categories
			FROM BinStorage bs
			JOIN BinName bn 
			ON bs.BinNameID = bn.BinNameID
			JOIN BinTags bt
			ON bt.BinStorageID = bs.BinStorageID
			WHERE bt.BinCategoryID IN (SELECT * FROM @TblCategoryId)
			GROUP BY bs.Descr, bn.Descr, bs.BinStorageID, bs.BinNameID
			HAVING COUNT(DISTINCT bt.BinCategoryID) = (SELECT COUNT(*) FROM @TblCategoryId)
		) AS subquery
		WHERE (BinNameID = @BinNameId OR @BinNameId = -999 OR @BinNameId = '')
			AND (
					@KeyWord = '' 
						OR 
					(ItemDescription LIKE '%' + @KeyWord + '%' OR Categories LIKE '%' + @KeyWord + '%')
				)
		ORDER BY BinName, ItemDescription, Categories
	END
	ELSE
	BEGIN
		SELECT ItemDescription, BinName, Categories, BinStorageID, BinNameID
		FROM (
			SELECT bs.Descr AS ItemDescription,
				   bn.Descr AS BinName,
				   bs.BinNameID,
				   bs.BinStorageID,
				   STUFF((SELECT ', ' + bc.Descr
						  FROM BinTags bt
						  JOIN BinCategory bc 
						  ON bc.BinCategoryID = bt.BinCategoryID
						  WHERE bt.BinStorageID = bs.BinStorageID
						  FOR XML PATH('')), 1, 2, '') AS Categories
			FROM BinStorage bs
			JOIN BinName bn 
			ON bs.BinNameID = bn.BinNameID
		) AS subquery
		WHERE (BinNameID = @BinNameId OR @BinNameId = -999 OR @BinNameId = '')
			AND (
					@KeyWord = '' 
						OR 
					(ItemDescription LIKE '%' + @KeyWord + '%' OR Categories LIKE '%' + @KeyWord + '%')
				)
		ORDER BY BinName, ItemDescription, Categories
	END

    RETURN 10

END TRY
BEGIN CATCH 

    DECLARE	@ErrorLine		INT				= ERROR_LINE(),
			@ErrorNumber	INT				= ERROR_NUMBER(),
			@ErrorMessage	VARCHAR(8000)	= ERROR_MESSAGE(),
			@ErrorProcedure	VARCHAR(255)	= 'DB.dbo.spBinStorageGridView;'
												+ COALESCE(ERROR_PROCEDURE(), '')

	EXEC dbo.spInsertSQLError @ErrorMessage, @ErrorNumber, @ErrorProcedure, @ErrorLine, @ParameterList

    RETURN 99

END CATCH
